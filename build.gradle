plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.31" apply false
    id "org.jetbrains.kotlin.plugin.serialization" version "1.4.31" apply false
    id "com.github.johnrengelman.shadow" version "5.2.0" apply false
}

allprojects {
    group "com.mediamod"
    version "1.0-SNAPSHOT"

    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "org.jetbrains.kotlin.plugin.serialization"
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        mavenCentral()
        maven { url "https://repo.sk1er.club/repository/maven-public" }
    }

    configurations {
        embed
        implementation.extendsFrom(embed)
    }

    dependencies {
        embed "org.jetbrains.kotlin:kotlin-stdlib"
        embed "org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0"
    }

    shadowJar {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations = [project.configurations.embed]

        setArchivesBaseName("MediaMod")
        destinationDirectory.set(new File(rootProject.projectDir, "build/libs"))
    }

    // Include dependencies in jar using shadow
    tasks.build.finalizedBy tasks.shadowJar
}

// Disable jar building for the root project
tasks.jar.enabled = false